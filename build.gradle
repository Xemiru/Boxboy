plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'ninja.miserable.blossom' version '1.0.1'
}

description 'https://github.com/Xemiru/Boxboy'
group 'com.github.xemiru.sponge'
version '1.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.0.0'
}

compileJava.dependsOn licenseFormatMain, licenseFormatTest

//

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task buildTest(type: Jar) {
    dependsOn compileTestJava

    baseName = "boxboy-test"
    from sourceSets.main.output.classesDir, sourceSets.test.output.classesDir
}

//

buildTest.dependsOn clean

blossom {
    def pluginFile = 'src/main/java/com/github/xemiru/sponge/boxboy/BoxboyPlugin.java'
    replaceToken '$VERSION$', "$project.version", pluginFile
}

license {
    header = project.file("LICENSE.txt")
    mapping {
        java = "SLASHSTAR_STYLE"
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonaUser, password: sonaPass)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                authentication(userName: sonaUser, password: sonaPass)
            }

            pom.project {
                name '$project.name'
                packaging 'jar'
                description 'https://github.com/Xemiru/Boxboy'
                url 'https://github.com/Xemiru/Boxboy'

                licenses {
                    license {
                        name 'The MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                
                scm {
                    connection 'scm:git:https://github.com/Xemiru/Boxboy.git'
                    developerConnection 'scm:git:https://github.com/Xemiru/Boxboy.git'
                    url 'scm:git:https://github.com/Xemiru/Boxboy'
                }
                
                developers {
                    developer {
                        id 'xemiru'
                        name 'Xemiru'
                        email 'xemiruk@gmail.com'
                    }
                }
            }
        }
    }
}
